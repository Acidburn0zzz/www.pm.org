#!/home/acme/bin/perl -w

use strict;
use File::Copy;
use Image::WorldMap;
use Template;
use XML::Simple;

my %colours = (
  'africa' => [64,255,64],
  'asia' => [255,255,0],
  'europe' => [128,128,255],
  'north_america' => [255,0,0],
  'central_america' => [0,255,0],
  'south_america' => [255,0,255],
  'oceania' => [0,255,255],
);

$| = 1;

my $xml = XMLin('./perl_mongers.xml', cache => 'storable', suppressempty => 1);
$xml = $xml->{group};

my $tt = Template->new({
  POST_CHOMP => 1,
  PRE_CHOMP => 1,
  TRIM => 1,
  EVAL_PERL => 1 ,
  INCLUDE_PATH => ['.', 'lib', 'src'],
  PROCESS => 'layout',
});

# Array containing all the group names order by continent
my %groups;
mkdir "www/groups/graphics";

my $worldmap = Image::WorldMap->new("titchy.png");

foreach my $name (keys %$xml) {
  my $group = $xml->{$name};
  my $id = $group->{id};
  my $status = $group->{status} || 'not-specified-in-xml-file';
#  print "$status\n";
  next unless $status eq 'active' || $status eq 'sleeping';
#  print "$id\n";
#  print $continent, "\n";
#  print "$name\n" if ref($continent);
  die "$name has no id\n" unless defined $id;
  if ($name eq 'MarsNeedsWomen.pm' ||
      $name eq 'Nomads.pm' ||
      $name eq 'PerlMonks.pm'
     ) {
    $group->{location}->{continent} = 'Non-geographical';
    $group->{location}->{country} = 'Non-geographical';
  }
  if ($name eq 'EU.pm') {
    $group->{location}->{country} = 'Non-geographical';
  }

  die "$name has no continent\n" unless $group->{location}->{continent};
  die "$name has no country\n" unless $group->{location}->{country};
#  next unless $id eq '0';
  $group->{name} = $name;
  $group->{location}->{all} = join ", ", 
    grep { defined }
    map { $group->{location}->{$_} }
 qw(city region state country continent);

  if ($group->{location}->{longitude}
      && $group->{location}->{latitude}) {

    # first add to world map
    my $c = lc $group->{location}->{continent};
    $c =~ s/\W/_/g;
    my $colour = $colours{$c} || die;
    $worldmap->add($group->{location}->{longitude}, $group->{location}->{latitude}, 'group', $colour);

    # have we drawn the single-country map already?
    if (!-f "www/groups/graphics/$id.png") {
      my $map = Image::WorldMap->new("titchy.png");
      $map->add($group->{location}->{longitude}, $group->{location}->{latitude}, 'group', $colour);
      $map->draw("foo.png");
      system("pngtopnm foo.png | ppmquant 64 | pnmtopng -compression 9 > www/groups/graphics/$id.png");
      $group->{image} = "graphics/$id.png";
    }
  } else {
    print "Group $name in $group->{location}->{all} missing location\n";
  }

  my $continent = $group->{location}->{continent};
  my $country = $group->{location}->{country};
  $group->{tsar}->{email}->{content} =~ s/@/ at /g;

  push @{$groups{$continent}->{$country}}, $group;

  if (!-f "www/groups/$id.html") {
    $tt->process('src/groups/group.html', $group, "www/groups/$id.html")
      || die $tt->error();
  }
}

copy("./perl_mongers.xml", "www/groups/perl_mongers.xml");
copy("./perl_mongers.dtd", "www/groups/perl_mongers.dtd");

#use YAML; die Dump(\%groups);
my @continents = (
  "Africa", "Asia", "Europe", "North America", "Central America",
  "South America", "Oceania", "Non-geographical",
);

foreach my $continent (@continents) {
  my $name = lc $continent;
  $name =~ s/\W/_/g;
  my $file = "www/groups/$name.html";
#  warn "$continent -> $file\n";
  my $groups;
  my $map = Image::WorldMap->new("titchy.png");
  foreach my $country (sort keys %{$groups{$continent}}) {
#    warn "  $country\n";
    my @mygroups = @{$groups{$continent}->{$country}};
    $groups->{$country} = [sort { lc $a->{name} cmp lc $b->{name} } @mygroups];
  }

  my $colour = $colours{$name};
  foreach my $name (keys %$xml) {
    my $group = $xml->{$name};
    my $status = $group->{status} || 'not-specified-in-xml-file';
    next unless $status eq 'active' || $status eq 'sleeping';
    next unless $group->{location}->{continent} eq $continent;
    if ($group->{location}->{longitude}
	&& $group->{location}->{latitude}) {
      $map->add($group->{location}->{longitude}, $group->{location}->{latitude}, "group", $colour);
    }
  }

  $map->draw("foo.png");
  system("pngtopnm foo.png | ppmquant 127 | pnmtopng -compression 9 > www/groups/graphics/$name.png");
  my $conf = {
    groups => $groups,
    continent => $continent,
  };
  $conf->{image} = "graphics/$name.png" unless $name eq 'non_geographical';

  $tt->process('src/groups/continent.html', $conf, $file)
    || die $tt->error();
#use YAML; die Dump($groups);
#use Data::Dumper; die Dumper($groups);
}

$worldmap->draw("foo.png");
system("pngtopnm foo.png | ppmquant 255 | pnmtopng -compression 9 > www/groups/graphics/world.png");

__END__
=head1 NAME

draw.pl - draw the master copies of the Perl Monger World Maps

=head1 SYNOPSIS

draw.pl [-huge]

=head1 DESCRIPTION

This uses the two large earth.png, earth-small.png, and the Perl Monger
Group XML file, perl_mongers.xml. It takes the longitude and latitude 
location information contained in the XML file and produces one of
two image files: mongers.png and mongers-small.png.

It also outputs a list of all the groups which do not current have
location information.

=head1 AUTHOR

Leon Brocard, leon@astray.com

=cut

__END__
Example data structure for a group:

$VAR1 = {
          'location' => {
                          'state' => {},
                          'country' => 'Ireland',
                          'latitude' => '52.664',
                          'region' => {},
                          'city' => 'Limerick',
                          'longitude' => '-8.623',
                          'continent' => 'Europe'
                        },
          'web' => {},
          'tsar' => {
                      'email' => {
                                   'content' => 'foranp@tinet.ie',
                                   'type' => 'personal'
                                 },
                      'name' => 'Paul Foran'
                    },
          'date' => {
                      'content' => '19990214',
                      'type' => 'inception'
                    },
          'id' => '134',
          'mailing-list' => {
                              'subscribe' => 'subscribe limerick-pm-list email_address',
                              'unsubscribe' => 'unsubscribe limerick-pm-list email_address',
                              'email' => [
                                           {
                                             'content' => 'limerick-pm-list@pm.org',
                                             'type' => 'list'
                                           },
                                           {
                                             'content' => 'majordomo@pm.org',
                                             'type' => 'list_admin'
                                           }
                                         ],
                              'name' => 'General Limerick.pm discussion'
                            },
          'email' => {
                       'type' => 'group'
                     }
        };
