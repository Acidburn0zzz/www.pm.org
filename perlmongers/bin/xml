#!/home/acme/bin/perl -w

use strict;
use File::Copy;
use Image::WorldMap;
use Template;
use XML::Simple;

$| = 1;

my $xml = XMLin('./perl_mongers.xml', cache => 'storable', suppressempty => 1);
$xml = $xml->{group};

my $tt = Template->new({
  POST_CHOMP => 1,
  PRE_CHOMP => 1,
  TRIM => 1,
  EVAL_PERL => 1 ,
  INCLUDE_PATH => ['.', 'lib', 'src'],
  PROCESS => 'layout',
});

# Array containing all the group names order by continent
my %groups;
mkdir "www/groups/graphics";

foreach my $name (keys %$xml) {
  my $group = $xml->{$name};
  my $id = $group->{id};
  my $status = $group->{status} || 'not-specified-in-xml-file';
#  print "$status\n";
  next unless $status eq 'active' || $status eq 'sleeping';
#  print "$id\n";
#  print $continent, "\n";
#  print "$name\n" if ref($continent);
  die "$name has no id\n" unless defined $id;
  if ($name eq 'MarsNeedsWomen.pm' ||
      $name eq 'Nomads.pm' ||
      $name eq 'PerlMonks.pm'
     ) {
    $group->{location}->{continent} = 'Non-geographical';
    $group->{location}->{country} = 'Non-geographical';
  }
  if ($name eq 'EU.pm') {
    $group->{location}->{country} = 'Non-geographical';
  }

  die "$name has no continent\n" unless $group->{location}->{continent};
  die "$name has no country\n" unless $group->{location}->{country};
#  next unless $id eq '0';
  $group->{name} = $name;
  $group->{location}->{all} = join ", ", 
    grep { defined }
    map { $group->{location}->{$_} }
 qw(city region state country continent);

  if (!-f "www/groups/graphics/$id.png"
      && $group->{location}->{longitude}
      && $group->{location}->{latitude}) {
    my $map = Image::WorldMap->new("titchy.png");
    $map->add($group->{location}->{longitude}, $group->{location}->{latitude});
    $map->draw("foo.png");
    system("pngtopnm foo.png | ppmquant 64 | pnmtopng -compression 9 > www/groups/graphics/$id.png");
    $group->{image} = "graphics/$id.png";
  }

  my $continent = $group->{location}->{continent};
  my $country = $group->{location}->{country};
  $group->{tsar}->{email}->{content} =~ s/@/ at /g;

  push @{$groups{$continent}->{$country}}, $group;

  if (!-f "www/groups/$id.html") {
    $tt->process('src/groups/group.html', $group, "www/groups/$id.html")
      || die $tt->error();
  }
}

copy("./perl_mongers.xml", "www/groups/perl_mongers.xml");
copy("./perl_mongers.dtd", "www/groups/perl_mongers.dtd");

#use YAML; die Dump(\%groups);
foreach my $continent (sort keys %groups) {
  my $file = lc $continent;
  $file =~ s/\W/_/g;
  $file = "www/groups/$file.html";
  warn "$continent -> $file\n";
  my $groups;
  foreach my $country (sort keys %{$groups{$continent}}) {
    warn "  $country\n";
    $groups->{$country} = [sort { lc $a->{name} cmp lc $b->{name} } @{$groups{$continent}->{$country}}];
  }
  my $conf = {
    groups => $groups,
    continent => $continent,
  };
  $tt->process('src/groups/continent.html', $conf, $file)
    || die $tt->error();
#use YAML; die Dump($groups);
#use Data::Dumper; die Dumper($groups);
}

__END__
=head1 NAME

draw.pl - draw the master copies of the Perl Monger World Maps

=head1 SYNOPSIS

draw.pl [-huge]

=head1 DESCRIPTION

This uses the two large earth.png, earth-small.png, and the Perl Monger
Group XML file, perl_mongers.xml. It takes the longitude and latitude 
location information contained in the XML file and produces one of
two image files: mongers.png and mongers-small.png.

It also outputs a list of all the groups which do not current have
location information.

=head1 AUTHOR

Leon Brocard, leon@astray.com

=cut

__END__
Example data structure for a group:

$VAR1 = {
          'location' => {
                          'state' => {},
                          'country' => 'Ireland',
                          'latitude' => '52.664',
                          'region' => {},
                          'city' => 'Limerick',
                          'longitude' => '-8.623',
                          'continent' => 'Europe'
                        },
          'web' => {},
          'tsar' => {
                      'email' => {
                                   'content' => 'foranp@tinet.ie',
                                   'type' => 'personal'
                                 },
                      'name' => 'Paul Foran'
                    },
          'date' => {
                      'content' => '19990214',
                      'type' => 'inception'
                    },
          'id' => '134',
          'mailing-list' => {
                              'subscribe' => 'subscribe limerick-pm-list email_address',
                              'unsubscribe' => 'unsubscribe limerick-pm-list email_address',
                              'email' => [
                                           {
                                             'content' => 'limerick-pm-list@pm.org',
                                             'type' => 'list'
                                           },
                                           {
                                             'content' => 'majordomo@pm.org',
                                             'type' => 'list_admin'
                                           }
                                         ],
                              'name' => 'General Limerick.pm discussion'
                            },
          'email' => {
                       'type' => 'group'
                     }
        };
