#!/usr/bin/perl -w

use strict;
use File::Copy;
use File::Find::Rule;
use File::Path;
use File::Spec::Functions qw(splitpath);
use Template;

my $tt = Template->new({
  PRE_PROCESS => 'macros',
  POST_CHOMP => 1,
  PRE_CHOMP => 1,
  TRIM => 1,
  EVAL_PERL => 1 ,
  INCLUDE_PATH => ['.', 'lib', 'src'],
});

my $source = 'src';
my $destination = 'www';
my $parms;

my $rule = File::Find::Rule->new;
$rule->or(
  $rule->new->directory->name('.svn')->prune->discard,
  $rule->new->directory->name('census')->prune->discard,
  $rule->new
);
my @files = $rule->file()->name(qr/^.[^~]+$/)->in($source);

foreach my $file (@files) {
  next if $file eq 'src/groups/index.html'; # processed by bin/xml
  
  print STDERR $file, "\n";

  my $destfile = $file;
  $destfile =~ s/^$source/$destination/;
  my($volume, $directories, $filepart) = splitpath($destfile);
  mkpath $directories;
  warn "$file -> $destfile\n";

  if ($file =~ /\.png$/) {
    copy($file, $destfile);
  } else {
    $tt->process($file, $parms, $destfile) || die $tt->error;
  }
}



