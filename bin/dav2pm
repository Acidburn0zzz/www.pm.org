#!/usr/bin/perl

use strict;
use warnings;

use HTTP::DAV;
use FindBin '$Bin';
use Getopt::Std;
use Term::ReadKey;
use File::Find::Rule;
use File::Basename qw(basename);

my %opt;
getopts('u:p:s:', \%opt);

unless ($opt{u}) {
  print 'username: ';
  chomp($opt{u} = <STDIN>);
}
unless ($opt{p}) {
  print 'password: ';
  ReadMode 'noecho';
  chomp($opt{p} = <STDIN>);
  ReadMode 'normal';
  print "\n";
}

my $start = $opt{s} || "$Bin/../www/";

if ($start !~ m|/$| and -d $start) {
  $start .= '/';
}

my $root = 'https://groups.pm.org/admin/groups';

my $dav = HTTP::DAV->new;

$dav->credentials($opt{u}, $opt{p}, $root);

$dav->open($root) or die "Unable to open $root: '" . $dav->message . "'\n";

if (-d $start) {
  foreach my $file (File::Find::Rule->file->relative->in($start)) {
    do_this("www/$file");
  }
} elsif (-f $start) {
  do_this($start); 
} else {
  die "not good. '$start' is not a directory nor a file\n";
}

sub do_this {
  my $file = shift;
  print "$file -> $root/$file\n";
  $dav->put($file, "$root/$file")
    or die "Unable to put $root/$file: '" . $dav->message . "'\n";
}

